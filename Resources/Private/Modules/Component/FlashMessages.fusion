prototype(Garagist.Mautic:Component.FlashMessages) < prototype(Neos.Fusion:Component) {
    flashMessages = ${[]}

    @if.hasMessages = ${Carbon.Array.check(this.flashMessages)}

    renderer = Neos.Fusion:Tag {
        attributes {
            id = 'neos-notification-container'
            class = 'neos-notification-top'
        }
        content = Neos.Fusion:Loop {
            items = ${props.flashMessages}
            itemRenderer = Neos.Fusion:Component {
                severity = ${String.toLowerCase(item.severity)}
                severity.@process {
                    replaceOKStatus = ${value == 'ok' ? 'success' : value}
                    replaceNoticeStatus = ${value == 'notice' ? 'info' : value}
                }

                title = ${item.title ? Translation.translate(item.title, item.title, item.arguments, 'Module', 'Garagist.Mautic') : null}
                message = ${item.message ? Translation.translate(item.message, item.message, item.arguments, 'Module', 'Garagist.Mautic') : null}
                renderer = afx`
                    <div class={'neos-notification neos-notification-' + props.severity} x-data="{expanded:false}">
                        <div
                            class={['neos-notification-content', props.message ? 'expandable' : null]}
                            :class="expanded&&'expanded'"
                        >
                            <i class={'fas fa-' + props.severity}></i>
                            <div @if.set={props.title && !props.message} class="neos-notification-heading">
                                {props.title}
                            </div>
                            <button
                                @if.set={props.title && props.message}
                                x-on:click="expanded=!expanded"
                                type="button"
                                class="neos-notification-heading text-left"
                            >
                                {props.title}
                            </button>
                            <div @if.set={props.message} class="neos-expand-content">
                                {props.message}
                            </div>
                        </div>
                    </div>
                `
            }
        }
    }
}

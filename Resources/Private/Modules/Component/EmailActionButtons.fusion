prototype(Garagist.Mautic:Component.EmailActionButtons) < prototype(Neos.Fusion:Component) {
    action = ${Configuration.Setting('Garagist.Mautic.action')}
    detailView = false

    _buttonClass = 'whitespace-nowrap neos-button !bg-neos-gray-medium '
    _default = 'hover:!bg-neos-blue focus:!bg-neos-blue'
    _canResend = ${this.action.resend && this.canResend}

    @if.hasPing = ${ping}

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        _actions = Neos.Fusion:DataStructure {
            @if.ping = ${ping}
            @process.filter = ${Array.filter(value)}
            mailProperties = afx`
                <Garagist.Mautic:Component.MailProperties
                    @if={props.action.edit && props.canEdit}
                    subject={props.subject}
                    segments={props.segments}
                    class={props._buttonClass + props._default}
                    detailView={props.detailView}
                    {...props.hrefArguments}
                />
            `
            sendTestMail = afx`
                <Garagist.Mautic:Component.SendTestMail
                    @if={(props.action.test && props.canTest) || (props.action.test && props._canResend)}
                    detailView={props.detailView}
                    {...props.hrefArguments}
                />
            `
            update = Neos.Fusion:DataStructure {
                @if.set = ${props.action.update && props.canUpdate}
                labelKey = 'email.update'
                action = 'update'
                icon = 'fa-sync'
                type = 'warning'
            }
            publish = Neos.Fusion:DataStructure {
                @if.set = ${props.action.publish && props.canPublish}
                labelKey = 'email.publish'
                action = 'publish'
                icon = 'fa-upload'
            }
            send = afx`
                <Garagist.Mautic:Component.SendMail
                    @if={(props.action.send && props.canSend) || props._canResend}
                    action='send'
                    resend={props.canResend}
                    detailView={props.detailView}
                    segments={props.segments}
                    {...props.hrefArguments}
                />
            `
            unpublish = Neos.Fusion:DataStructure {
                @if.set = ${props.action.unpublish && props.canUnpublish}
                labelKey = 'email.unpublish'
                action = 'unpublish'
                icon = 'fa-download'
            }
            publishAndSend = afx`
                <Garagist.Mautic:Component.SendMail
                    @if={props.action.publishAndSend && props.canPublishAndSend}
                    action='publishAndSend'
                    detailView={props.detailView}
                    segments={props.segments}
                    {...props.hrefArguments}
                />
            `
            unpublishAndUpdate = Neos.Fusion:DataStructure {
                @if.set = ${props.action.unpublishAndUpdate && props.canUnpublishAndUpdate}
                labelKey = 'email.update'
                action = 'unpublishAndUpdate'
                icon = 'fa-sync'
            }
            unlock =  Neos.Fusion:DataStructure {
                @if.set = ${props.isFailed}
                labelKey = 'task.unlock'
                action = 'unlock'
                type = 'warning'
                icon = 'fa-unlock'
            }
            delete = Neos.Fusion:DataStructure {
                @if.set = ${props.action.delete && props.canDelete}
                labelKey = 'email.delete'
                action = 'delete'
                icon = 'fa-trash'
                type = 'danger'
                confirm = true
            }
        }

        renderer = afx`
            <Neos.Fusion:Loop items={props._actions}>
                {Type.isString(item) ? item : ''}
                <Garagist.Mautic:Component.ActionButton
                    @if={!Type.isString(item)}
                    showText={props.detailView}
                    actionArguments={props.hrefArguments}
                    {...item}
                />
            </Neos.Fusion:Loop>
        `
    }
}

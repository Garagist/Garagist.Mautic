prototype(Garagist.Mautic:Component.EmailData) < prototype(Neos.Fusion:Component) {
    email = null
    node = null
    redirect = 'node'

    @if.hasEmail = ${this.email}

    renderer = Neos.Fusion:DataStructure {
        name = ${props.email.name}
        dateCreated = ${Date.format(props.email.dateCreated, 'd.m.Y H:m:s')}
        emailIdentifier = ${props.email.emailIdentifier}
        properties = ${props.email.properties}
        subject = ${this.properties ? this.properties.subject : null}
        segments = ${this.properties ? this.properties.segments : null}
        dateSent = ${props.email.dateSent ? Date.format(props.email.dateSent, 'd.m.Y H:m:s') : 'â€“'}
        hrefArguments = Neos.Fusion:DataStructure {
            node = ${props.node}
            email = ${props.email}
            redirect = ${props.redirect}
        }
        task = ${props.email.task}

        isIdle = ${props.email.task == 'idle'}
        isSended = ${props.email.dateSent}
        canPublish = ${this.isIdle && (props.email.dateModified != null && props.email.dateSend == null && !props.email.published)}
        canUnpublish = ${this.isIdle && (props.email.dateSent == null && props.email.published)}
        canTest = ${this.canPublish || this.canUnpublish}
        canEdit = ${this.canPublish}
        canSend = ${this.isIdle && (props.email.dateSent == null && props.email.published)}
        canUpdate = ${this.isIdle && this.properties && (props.email.dateSent == null && !props.email.published)}
        canDelete= ${this.isIdle}
        canPublishAndSend = ${this.isIdle && (props.email.dateModified != null && props.email.dateSend == null)}
        canUnpublishAndUpdate = ${this.isIdle && (props.email.dateSent == null)}

        isRunning = ${props.email.task != 'idle' && props.email.task != 'failed'}
        isFailed = ${props.email.task == 'failed'}
    }
}

prototype(Garagist.Mautic:Component.SendMail) < prototype(Neos.Fusion:Component) {
    email = null

    // Redirect after the action is done
    redirect = null
    action = 'send'

    class = 'neos-button neos-button-primary'
    footer = false
    allSegments = ${allSegments}

    @if.hasEmail = ${this.email}

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        _i18n = ${Translation.id('').package('Garagist.Mautic').source('Module')}
        _label = ${this._i18n.id(props.resend ? 'email.resend' : 'email.send').translate()}
        _actionUri = Neos.Fusion:UriBuilder {
            action = ${props.action}
            arguments = Neos.Fusion:DataStructure {
                node = ${node}
                email = ${props.email}
                redirect = ${props.redirect}
            }
        }

        _confirmAttributes = Neos.Fusion:DataStructure {
            :href = "action+'&moduleArguments[date]='+date"
        }

        _sendDateLabel = ${this._i18n.id('email.send.date').translate()}
        _locale = Neos.Fusion:Match {
            @subject = ${Carbon.Backend.language()}
            @default = 'en'
            de = 'de'
        }
        _hasSegments = ${Carbon.Array.check(props.segments)}
        // class in xlf file: 'block' Don't remove this as it is needed for tailwindcss to get the needed class name

        renderer = afx`
            <Garagist.Mautic:Component.ActionButton
                label={props._label}
                icon="fa-paper-plane"
                showText={props.detailView}
                dialogTitle={this.label}
                dialogXData={"{action:'" + props._actionUri + "',locale:'" + props._locale + "',date:'now',sendLater:false,pickr:null,formattedDate:''}"}
                dialogxEffect="if(date!='now'){formattedDate=new Date(date).toLocaleDateString(locale, { weekday: 'long', year: 'numeric', month: 'long', day: 'numeric', hour: 'numeric', minute: 'numeric' })}"
                confirmLabel={this.label}
                confirmAttributes={props._confirmAttributes}
                footer={props.footer}
                initalFocusOnConfirm={true}
                type="success"
                dialogMaxWidth="382px"
            >
                <p x-dialog:description @if={!props._hasSegments}>
                    <span x-show="date=='now'">{props._i18n.id('email.send.now.all').translate()}</span>
                    <span x-show="date!='now'">{props._i18n.id('email.send.later.all').translate()}</span>
                </p>
                <p x-dialog:description @if={props._hasSegments}>
                    <span x-show="date=='now'">{props._i18n.id('email.send.now.segments').translate()} </span>
                    <span x-show="date!='now'">{props._i18n.id('email.send.later.segments').translate()} </span>
                    <Neos.Fusion:Loop items={props.segments} @glue=", " content={props.allSegments[item].name} />
                </p>
                <Garagist.Mautic:Component.Checkbox
                    xModel="sendLater"
                    label={props._i18n.id('email.send.later').translate()}
                    value={true}
                />

                <div x-show="sendLater" x-collapse x-effect="date=sendLater&&pickr?pickr:'now'">
                    <label for="date" class="sr-only">{props._sendDateLabel}</label>
                    <input
                        x-show="sendLater"
                        x-model="pickr"
                        x-flatpickr="{locale}"
                        placeholder={props._sendDateLabel}
                        id="date"
                        class="sr-only"
                    />
                    <!-- Spacer for a smoother animation -->
                    <div aria-hidden="true"  class="h-4"></div>
                </div>
            </Garagist.Mautic:Component.ActionButton>
        `
    }
}

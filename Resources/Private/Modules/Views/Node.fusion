Garagist.Mautic.BackendController.node = Neos.Fusion:Component {
    title = ${q(node).property('title')}
    categories = Neos.Fusion:Map {
        items = ${categoryNodes || []}
        itemRenderer = Neos.Fusion:DataStructure {
            title = ${q(item).property('title')}
            node = ${item}
        }
    }

    emails = Neos.Fusion:Map {
        items = ${emails}
        itemRenderer = Garagist.Mautic:Component.EmailData {
            node = ${node}
            email = ${item}
        }
    }

    _i18n = ${Translation.id('').package('Garagist.Mautic').source('Module')}

    renderer = afx`
        <main class="mautic">
            <Garagist.Mautic:Component.FlashMessages />
            <section class="mautic-list">
                <h1 class="text-2xl mb-4">
                    {props._i18n.id('newsletter.headline').translate()}
                    <Neos.Fusion:Loop items={props.categories}>
                        <Neos.Fusion:Link.Action
                            href.action="link"
                            href.arguments.node={item.node}
                            class="hover:!underline focus:!underline"
                            target="_blank"
                            content={item.title}
                        />
                        {' â€º '}
                    </Neos.Fusion:Loop>
                    <Neos.Fusion:Link.Action
                        href.action="link"
                        href.arguments.node={node}
                        class="hover:!underline focus:!underline"
                        target="_blank"
                        content={props.title}
                    />
                </h1>
                <table class="neos-table" x-data x-tooltips>
                    <thead>
                        <tr>
                            <td>{props._i18n.id('status').translate()}</td>
                            <td>{props._i18n.id('subject').translate()}</td>
                            <td>{props._i18n.id('createdOn').translate()}</td>
                            <td>{props._i18n.id('sentOn').translate()}</td>
                            <td>{props._i18n.id('recipients').translate()}</td>
                            <td class="!text-right">{props._i18n.id('actions').translate()}</td>
                        </tr>
                    </thead>
                    <tbody>
                        <Neos.Fusion:Loop items={props.emails}>
                            <tr>
                                <td width="1%" class="!text-center whitespace-nowrap">
                                    <span
                                        @if={item.isFailed}
                                        class="inline-block"
                                        aria-label={props._i18n.id('task.failed').translate()}
                                    >
                                        <i class="fas fa-exclamation-triangle text-red-500"></i>
                                    </span>
                                    <i @if={!item.isFailed} class="fas fa-check text-green-600"></i>
                                </td>
                                <td
                                    @if={item.subject}
                                    width="1%"
                                    class="whitespace-nowrap !p-0"
                                >
                                    <Neos.Fusion:Link.Action
                                        class="neos-button neos-button-primary block text-left"
                                        href.action="detail"
                                        href.arguments={item.hrefArguments}
                                        aria-label={'ID: ' + item.emailIdentifier}
                                    >
                                        {item.subject}
                                    </Neos.Fusion:Link.Action>
                                </td>
                                <td
                                    @if={!item.subject}
                                    width="1%"
                                    class="whitespace-nowrap"
                                    aria-label={'ID: ' + item.emailIdentifier}
                                >
                                    {props.title}
                                </td>
                                <td>{item.dateCreated}</td>
                                <td>
                                    <Neos.Fusion:Loop items={item.sent}>
                                        <span aria-label={props._i18n.id('dateAndTime').arguments([Date.format(item, 'd.m.Y'), Date.format(item, 'H:m:s')]).translate()}>
                                            {Date.format(item, 'd.m.Y')}
                                        </span>
                                        {iterator.isLast ? '' : ', '}
                                    </Neos.Fusion:Loop>
                                </td>
                                <td>{item.properties.recipients}</td>
                                <td class="neos-action">
                                    <div
                                        x-data="actions(2)"
                                        class="neos-pull-right"
                                    >
                                        <Garagist.Mautic:Component.EmailActionButtons {...item} />
                                        <Neos.Fusion:Link.Action
                                            @if={item.properties}
                                            class="neos-button neos-button-primary"
                                            href.action="detail"
                                            href.arguments={item.hrefArguments}
                                            aria-label={props._i18n.id('email.detail').translate()}
                                        >
                                            <i class="fas fa-info icon-white"></i>
                                        </Neos.Fusion:Link.Action>
                                    </div>
                                </td>
                            </tr>
                        </Neos.Fusion:Loop>
                    </tbody>
                </table>
            </section>
        </main>
        <div class="neos-footer mautic">
            <Neos.Fusion:Link.Action
                class="neos-button neos-button-neos-button-secondary"
                href.action="index"
            >
                {props._i18n.id('back').translate()}
            </Neos.Fusion:Link.Action>
            <Neos.Fusion:Link.Action
                class="neos-button neos-button-secondary"
                href.action="node"
                href.arguments.node={node}
            >
                {props._i18n.id('list.refresh')}
            </Neos.Fusion:Link.Action>
            <Garagist.Mautic:Component.MailProperties />
        </div>
    `
}

Garagist.Mautic.BackendController.index = Neos.Fusion:Component {
    ping = ${ping}
    pages = ${pages}
    categories = ${categories}
    i18n = ${Translation.id('').package('Garagist.Mautic').source('Module')}
    hasPages = ${Carbon.Array.check(this.pages)}
    hasCategories = ${Carbon.Array.check(this.categories)}
    showPagesWithoutEmails = ${this.hasPages && Array.some(this.pages, page => page.count > 0) ? 'false' : 'true'}
    logo = ${this.ping ? 'Logo.svg' : 'LogoInactive.svg'}

    renderer = afx`
        <main class="mautic" x-data={"{showPagesWithoutEmails:" + props.showPagesWithoutEmails + "}"}>
            <h1>
                <span class="sr-only">Mautic</span>
                <span
                    class="inline-block"
                    aria-label={props.i18n.id(props.ping ? 'status.active' : 'status.inactive').translate()}
                    data-balloon-pos="up"
                >
                    <img width="152" alt="Mautic Logo" aria-hidden="true" src={StaticResource.uri("Garagist.Mautic", "Public/Assets/" + props.logo)} />
                </span>
            </h1>
            <h2 class="sr-only">{props.i18n.id('list.emails.header').translate()}</h2>
            <Garagist.Mautic:Component.ToggleSwitch
                @if.set={props.showPagesWithoutEmails == 'false'}
                toggle="showPagesWithoutEmails"
                label="list.showPagesWithoutEmails"
            />

            <p @if.set={!props.hasPages}>{props.i18n.id('list.emails.none').translate()}</p>

            <Neos.Fusion:Loop @if.set={props.hasCategories} items={props.categories}>
                <div
                    class={[iterator.isFirst ? null : "xl:mt-16", "mt-12"]}
                    x-show={item.main.count || Array.some(item.pages, page => page.count > 0) ? null : 'showPagesWithoutEmails'}
                >
                    <h3 @if.set={!item.main} class="text-xl mb-4">{item.title}</h3>
                    <Garagist.Mautic:Component.Index
                        @if.set={item.main}
                        class="mb-4 text-xl"
                        show={null}
                        {...item.main}
                    />
                    <div class="grid grid-cols-1 gap-4 md:grid-cols-2 lg:grid-cols-3 xl:grid-cols-4">
                        <Neos.Fusion:Loop items={item.pages}>
                            <Garagist.Mautic:Component.Index {...item} />
                        </Neos.Fusion:Loop>
                    </div>
                </div>
            </Neos.Fusion:Loop>

            <div class="mt-8 grid grid-cols-1 gap-4 md:grid-cols-2 lg:grid-cols-3 xl:grid-cols-4" @if.set={!props.hasCategories && props.hasPages}>
                <Neos.Fusion:Loop items={props.pages}>
                    <Garagist.Mautic:Component.Index {...item} />
                </Neos.Fusion:Loop>
            </div>
            
        </main>
    `
}


prototype(Garagist.Mautic:Component.Index) < prototype(Neos.Fusion:Component) {
    count = null
    title = null
    class = 'text-sm'
    show = ${this.count ? null : 'showPagesWithoutEmails'}

    renderer = afx`
        <div x-show={props.show} class={[props.class, "relative rounded bg-neos shadow-sm flex items-center space-x-3 hover:bg-neos-darker focus-within:bg-neos-darker"]}>
            <div class={[props.count ? "bg-green-800" : "bg-red-800" ,"leading-none flex-shrink-0 h-16 w-16 rounded-l flex flex-col items-center justify-center"]}>
                <span class="text-lg">{props.count}</span>
                <span class="text-xs">
                    {Translation.translate('list.emails.count', null, [], 'Module', 'Garagist.Mautic', props.count)}
                </span>
            </div>
            <div class="flex-1 min-w-0">
                <Neos.Fusion:Link.Action
                    class="focus:outline-none font-medium text-gray-200"
                    href.action="email"
                    href.controller="Backend"
                    href.arguments.node={props.node}
                >
                    <span class="absolute inset-0" aria-hidden="true"></span>
                    {props.title}
                </Neos.Fusion:Link.Action>
            </div>
        </div>
    `
}

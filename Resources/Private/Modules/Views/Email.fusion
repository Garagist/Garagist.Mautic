Garagist.Mautic.BackendController.email = Neos.Fusion:Component {

    emails = ${emails}
    node = ${node}
    flashMessages = ${flashMessages}

    severityMapping = Neos.Fusion:DataStructure {
        OK = 'success'
        Notice = 'notice'
        Warning = 'warning'
        Error = 'error'
    }

    title = ${q(node).property('title')}
    titleOverride = ${q(node).property('titleOverride')}
    i18n = ${Translation.id('').package('Garagist.Mautic').source('Module')}

    createAction = Neos.Fusion:UriBuilder {
        action = 'create'
        arguments.node = ${node}
    }

    placeholder = ${this.titleOverride || this.title}

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        _emails = Neos.Fusion:Map {
            items = ${props.emails}
            itemRenderer = Garagist.Mautic:Component.EmailData {
                node = ${props.node}
                email = ${item}
            }
        }

        renderer = afx`
            <main class="mautic">
                <Garagist.Mautic:Component.FlashMessages flashMessages={props.flashMessages} />
                <section class='mautic-list'>
                    <h1 class="text-xl font-bold mb-4">
                        {props.i18n.id('emails.headline').arguments([props.title]).translate()}
                    </h1>
                    <table class='neos-table' x-data x-tooltips>
                        <thead>
                            <tr>
                                <td>{props.i18n.id('email.status').translate()}</td>
                                <td>{props.i18n.id('email.subject').translate()}</td>
                                <td>{props.i18n.id('email.createdOn').translate()}</td>
                                <td>{props.i18n.id('email.sentOn').translate()}</td>
                                <td class="!text-right">{props.i18n.id('email.actions').translate()}</td>
                            </tr>
                        </thead>
                        <tbody>
                        <Neos.Fusion:Loop items={props._emails}>
                            <tr>
                                <td width="1%" class="!text-center whitespace-nowrap">
                                    <span
                                        @if.set={item.isFailed}
                                        class="inline-block"
                                        aria-label={props.i18n.id('email.task.failed').translate()}
                                    >
                                        <i class="fas fa-exclamation-triangle text-red-500"></i>
                                    </span>
                                    <i @if.set={!item.isFailed} class="fas fa-check text-green-600"></i>
                                </td>
                                <td width="1%" class="whitespace-nowrap !p-0">
                                    <Neos.Fusion:Link.Action
                                        class="neos-button neos-button-primary block text-left"
                                        href.action="info"
                                        href.arguments={item.hrefArguments}
                                        aria-label={'ID: ' + item.emailIdentifier}
                                    >
                                        {item.subject}
                                    </Neos.Fusion:Link.Action>
                                </td>
                                <td>{item.dateCreated}</td>
                                <td>{item.dateSent}</td>
                                <td class="neos-action">
                                    <div class="neos-pull-right" x-data="actions(2)">
                                        <Garagist.Mautic:Component.EmailActionButtons {...item} />
                                        <Neos.Fusion:Link.Action
                                            class="neos-button neos-button-primary"
                                            href.action="info"
                                            href.arguments={item.hrefArguments}
                                            aria-label={props.i18n.id('list.email.info').translate()}
                                        >
                                            <i class="fas fa-info icon-white"></i>
                                        </Neos.Fusion:Link.Action>
                                    </div>
                                </td>
                            </tr>
                        </Neos.Fusion:Loop>
                        </tbody>
                    </table>
                </section>
            </main>
            <div class="neos-footer">
                <button class="neos-button neos-button-primary" data-toggle="modal" href="#create">
                    {props.i18n.id('email.action.create').translate()}
                </button>
                <Neos.Fusion:Link.Action
                    class="neos-button neos-button-secondary"
                    href.action="email"
                    href.arguments.node={props.node}
                >
                    {props.i18n.id('email.action.refresh')}
                </Neos.Fusion:Link.Action>
            </div>
            <div class="neos-hide mautic" id="create">
                <div class="neos-modal-centered">
                    <div class="neos-modal-content" x-data={"{action:'" + props.createAction + "', subject:''}"}>
                        <div class="neos-modal-header">
                            <button type="button" class="neos-close neos-button" data-dismiss="modal"></button>
                            <div class="neos-header">
                                {props.i18n.id('email.action.create').translate()}
                            </div>
                            
                            <fieldset class="mx-4">
                                <label for="title">Titel</label>
                                <input
                                    type="text"
                                    x-model="subject"
                                    id="title"
                                    placeholder={props.placeholder}
                                    class="block w-full"
                                />
                            </fieldset>
                        </div>
                        <div class="neos-modal-footer">
                            <button type="button" class="neos-button" data-dismiss="modal">
                                {Translation.translate('cancel', null, [], 'Main', 'Neos.Neos')}
                            </button>
                            <a :href="action + '&moduleArguments[subject]=' + subject" class="neos-button neos-button-primary">
                                {props.i18n.id('email.action.create').translate()}
                            </a>
                        </div>
                    </div>
                </div>
                <div class="neos-modal-backdrop neos-in"></div>
            </div>
        `
    }
}

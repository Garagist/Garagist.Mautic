Garagist.Mautic.BackendController.email = Neos.Fusion:Component {

    emails = ${emails}
    node = ${node}
    flashMessages = ${flashMessages}

    severityMapping = Neos.Fusion:DataStructure {
        OK = 'success'
        Notice = 'notice'
        Warning = 'warning'
        Error = 'error'
    }

    title = ${q(node).property('title')}
    i18n = ${Translation.id('').package('Garagist.Mautic').source('Module')}

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        _emails = Neos.Fusion:Map {
            items = ${props.emails}
            itemRenderer = Neos.Fusion:DataStructure {
                dateCreated = ${Date.format(item.dateCreated, 'd.m.Y h:m:s')}
                emailIdentifier = ${item.emailIdentifier}
                dateSent = ${item.dateSent ? Date.format(item.dateSent, 'd.m.Y h:m:s') : 'â€“'}
                hrefArguments = Neos.Fusion:DataStructure {
                    node = ${props.node}
                    email = ${item}
                }
                task = ${item.task}

                isIdle = ${item.task == 'idle'}
                canPublish = ${this.isIdle && (item.dateModified != null && item.dateSend == null && !item.published)}
                canUnpublish = ${this.isIdle && (item.dateSent == null && item.published)}
                canSend = ${this.isIdle && (item.dateSent == null && item.published)}
                canUpdate = ${this.isIdle && (item.dateSent == null && !item.published)}

                isRunning = ${item.task != 'idle' && item.task != 'failed'}
                isFailed = ${item.task == 'failed'}
            }
        }

        renderer = afx`
            <main class="mautic">
                <Garagist.Mautic:Component.FlashMessages flashMessages={props.flashMessages} />
                <section class='mautic-list'>
                    <h2 class="text-xl font-bold mb-4">
                        {props.i18n.id('emails.headline').arguments([props.title]).translate()}
                    <table class='neos-table' x-data x-tooltips>
                        <thead>
                            <tr>
                                <td>{props.i18n.id('email.status').translate()}</td>
                                <td>{props.i18n.id('email.identifier').translate()}</td>
                                <td>{props.i18n.id('email.createdOn').translate()}</td>
                                <td>{props.i18n.id('email.sentOn').translate()}</td>
                                <td class="!text-right">{props.i18n.id('email.actions').translate()}</td>
                            </tr>
                        </thead>
                        <tbody>
                        <Neos.Fusion:Loop items={props._emails}>
                            <tr>
                                <td width="1%" class="!text-center whitespace-nowrap">
                                    <span
                                        @if.set={item.isFailed}
                                        class="inline-block"
                                        aria-label={props.i18n.id('email.task.failed').translate()}
                                    >
                                        <i class="fas fa-exclamation-triangle text-red-500"></i>
                                    </span>
                                    <i @if.set={!item.isFailed} class="fas fa-check text-green-600"></i>
                                </td>
                                <td width="1%" class="whitespace-nowrap !p-0">
                                    <Neos.Fusion:Link.Action
                                        class="neos-button neos-button-primary block text-left"
                                        href.action="info"
                                        href.arguments={item.hrefArguments}
                                        aria-label={props.i18n.id('list.email.info').translate()}
                                        data-balloon-length="medium"
                                        data-balloon-pos="up"
                                    >
                                        {item.emailIdentifier}
                                    </Neos.Fusion:Link.Action>
                                </td>
                                <td>{item.dateCreated}</td>
                                <td>{item.dateSent}</td>
                                <td class="neos-action">
                                    <div class="neos-pull-right">
                                        <Neos.Fusion:Link.Action
                                            @if.set={item.canPublish}
                                            class="neos-button neos-button-primary"
                                            href.action="publish"
                                            href.arguments={item.hrefArguments}
                                            aria-label={props.i18n.id('list.email.publish').translate()}
                                            data-balloon-pos="up"
                                        >
                                            <i class="fas fa-upload"></i>
                                        </Neos.Fusion:Link.Action>
                                        <Neos.Fusion:Link.Action
                                            @if.set={item.canUnpublish}
                                            class="neos-button neos-button-primary"
                                            href.action="unPublish"
                                            href.arguments={item.hrefArguments}
                                            aria-label={props.i18n.id('list.email.unpublish').translate()}
                                            data-balloon-length="medium"
                                            data-balloon-pos="up"
                                        >
                                            <i class="fas fa-download"></i>
                                        </Neos.Fusion:Link.Action>
                                        <Neos.Fusion:Link.Action
                                            @if.set={item.canSend}
                                            class="neos-button neos-button-danger"
                                            href.action="send"
                                            href.arguments={item.hrefArguments}
                                            aria-label={props.i18n.id('list.email.send').translate()}
                                            data-balloon-pos="up"
                                        >
                                            <i class="fas fa-paper-plane"></i>
                                        </Neos.Fusion:Link.Action>
                                        <Neos.Fusion:Link.Action
                                            @if.set={item.canUpdate}
                                            class="neos-button neos-button-primary"
                                            href.action="update"
                                            href.arguments={item.hrefArguments}
                                            aria-label={props.i18n.id('list.email.update').translate()}
                                            data-balloon-pos="up"
                                        >
                                            <i class="fas fa-sync icon-white"></i>
                                        </Neos.Fusion:Link.Action>
                                        <Neos.Fusion:Link.Action
                                            class="neos-button neos-button-primary"
                                            href.action="info"
                                            href.arguments={item.hrefArguments}
                                            aria-label={props.i18n.id('list.email.info').translate()}
                                        >
                                            <i class="fas fa-info icon-white"></i>
                                        </Neos.Fusion:Link.Action>
                                    </div>
                                </td>
                            </tr>
                        </Neos.Fusion:Loop>
                        </tbody>
                    </table>
                </section>
            </main>
            <div class="neos-footer">
                <Neos.Fusion:Link.Action
                    class="neos-button neos-button-primary"
                    href.action="create"
                    href.arguments.node={props.node}
                >
                    {props.i18n.id('email.action.create').translate()}
                </Neos.Fusion:Link.Action>
                <Neos.Fusion:Link.Action
                    class="neos-button neos-button-secondary"
                    href.action="email"
                    href.arguments.node={props.node}
                >
                    {props.i18n.id('email.action.refresh')}
                </Neos.Fusion:Link.Action>
            </div>
        `
    }
}

Garagist.Mautic.BackendController.info = Neos.Fusion:Component {
    email = ${email}
    node = ${node}
    history = ${history}
    mauticRecord = ${mauticRecord}
    i18n = ${Translation.id('').package('Garagist.Mautic').source('Module')}

    renderer = afx`
        <main class="mautic">
            <div class="space-y-4">
                <section>
                    <h1 class="text-xl">{props.i18n.id('info.metadata').translate()}</h1>
                    <table class="neos-info-table">
                        <tbody>
                            <tr>
                                <th>{props.i18n.id('info.name').translate()}</th>
                                <td>{props.mauticRecord.name}</td>
                            </tr>
                            <tr>
                                <th>{props.i18n.id('info.subject').translate()}</th>
                                <td>{props.mauticRecord.subject}</td>
                            </tr>
                        </tbody>
                    </table>
                </section>
                <section>
                    <h1 class="text-xl">{props.i18n.id('info.history').translate()}</h1>
                    <table class="neos-info-table">
                        <thead>
                            <th>{props.i18n.id('info.date').translate()}</th>
                            <th>{props.i18n.id('info.type').translate()}</th>
                            <th>{props.i18n.id('info.message').translate()}</th>
                        </thead>
                        <tbody>
                            <Neos.Fusion:Loop items={props.history}>
                                <tr @if.render={item.type != 'Garagist.Mautic:MauticEmailTaskFinished' || item.error}>
                                    <td>{Date.format(item.date, 'd.m.Y h:m:s')}</td>
                                    <td>{item.type}</td>
                                    <td>{item.message}</td>
                                </tr>
                            </Neos.Fusion:Loop>
                        </tbody>
                    </table>
                </section>
            </div>

            <h1 class="text-xl mt-8 mb-4">{props.i18n.id('info.content').translate()}</h1>
            <div class="grid gap-8 grid-flow-row auto-rows-auto lg:grid-cols-2">
                <div>
                    <h2 class="text-md mb-4">{props.i18n.id('info.content.html').translate()}</h2>
                    <iframe
                        srcdoc={props.mauticRecord.customHtml}
                        class="bg-white w-full h-[calc(100vh-150px)]"
                    ></iframe>
                </div>
                <div>
                    <h2 class="text-md mb-4">{props.i18n.id('info.content.plaintext').translate()}</h2>
                    <pre class="block bg-slate-200 text-neos-dark px-8 py-12 overflow-auto h-[calc(100vh-150px)] text-xs md:text-sm lg:text-xs xl:text-sm">
                        {props.mauticRecord.plainText}
                    </pre>
                </div>
                <Neos.Fusion:Debug.Console value={props.mauticRecord} />
            </div>
        </main>
        <div class="neos-footer">
            <Neos.Fusion:Link.Action
                class="neos-button neos-button-primary"
                href.action="email"
                href.arguments.node={props.node}
                href.arguments.email={props.email}
            >
                {props.i18n.id('back').translate()}
            </Neos.Fusion:Link.Action>
            <Neos.Fusion:Link.Action
                class="neos-button neos-button-secondary"
                href.action="info"
                href.arguments.node={props.node}
                href.arguments.email={props.email}
            >
                {props.i18n.id('info.action.refresh')}
            </Neos.Fusion:Link.Action>
            <Neos.Fusion:Link.Action @if.error={Array.last(props.history).error} class="neos-button neos-button-warning" href.action="unlock" href.arguments.node={props.node} href.arguments.email={props.email}>
                {props.i18n.id('info.action.unlock').translate()}
            </Neos.Fusion:Link.Action>
        </div>
    `
}
